int n = 20;
int numberOfBombs = 40;
float w, h;
boolean over;

Tile[][] board = new Tile[n][n];

/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/

void setBombCount(int row, int col){
  if(board[row][col].isBomb){
    board[row][col].nearBombs = -1;
    return;
  }
  
  int count = 0;
  for(int m = row - 1; m <= row + 1; m++){
    for(int n = col - 1; n <= col + 1; n++){
      
      if(m >= 0 && m < board.length && n >= 0 && n < board.length && board[m][n].isBomb){
        count++;
      }
      
    }
  }
  
  board[row][col].nearBombs = count;
}

/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/

void setGame(){
  for(int i = 0; i<n; i++){
    for(int j=0; j<n; j++){
      setBombCount(i, j);
    }
  }
}

/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/

void plantBombs(){
  int total = 0;
  int row, col;
  
  do{
    do{
      row = (int) random(0,n);
      col = (int) random(0, n);
    }while(board[row][col].isBomb);
    
    board[row][col].turnBomb();
    total++;
  }while(total < numberOfBombs);
}

/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/

void revealCell(int row, int col){
  Tile temp = board[row][col];
  temp.reveal();
  
  if(temp.nearBombs == 0){
    for(int m = row - 1; m <= row + 1; m++){
      for(int n = col - 1; n <= col + 1; n++){
        
        if(m >= 0 && m < board.length && n >= 0 && n < board.length && !board[m][n].revealed){
          revealCell(m, n);
        }
        
      }
    }
  }
}

/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/

void setup(){
  size(600, 600);
  w = (float) Math.floor(width/n);
  h = (float) Math.floor(height/n);
  
  for(int i = 0; i<n; i++){
    for(int j=0; j<n; j++){
      board[i][j] = new Tile(i,j);
    }
  }
 
  plantBombs();
  setGame();
  textSize(20);
}

/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/

void draw(){
  background(0);
  
  for(Tile[] arr : board){
    for(Tile tile : arr){
      tile.show();
    }
  }

  if(!over) checkVictory();
}

/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/

void mousePressed(){
  int x = (int) mouseX;
  int y = (int) mouseY;
  int col = (int) y/ (int) w;
  int row = (int) x/ (int) h;
  if(mouseButton == LEFT){
    
    if(!over) revealCell(row, col);
    
    if(board[row][col].isBomb){
      println("Você Acertou uma Bomba!!");
      for(Tile[] arr : board){
        for(Tile tile : arr){
          tile.reveal();
        }
      }
      over = true;
    }
  }else if(mouseButton == RIGHT){
    if(board[row][col].suspectOfBomb){
      board[row][col].suspectOfBomb = false;
      board[row][col].revealed = false;
    }else{
      board[row][col].suspectOfBomb = true;
      board[row][col].reveal();
    }
  }
}

/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/
/****************************************************************************************************************************/

void checkVictory(){
  int revealed = 0;
  
  for(Tile[] arr : board){
    for(Tile tile : arr){
      if(tile.revealed){
        revealed++;
      }
    }
  }
  
  if(revealed >= n*n - numberOfBombs){
    println("Parabéns, Você Ganhou!!");
    over = true;
  }
}
